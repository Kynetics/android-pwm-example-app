import java.util.function.Predicate

/*
 * Copyright © 2020 – 2024  Kynetics, Inc.
 * SPDX-License-Identifier: Apache-2.0
 */

plugins {
    alias(libs.plugins.android.application)
}

android {
    namespace 'com.kynetics.android_pwm_example_app'
    compileSdk 34

    defaultConfig {
        applicationId "com.kynetics.android_pwm_example_app"
        minSdkVersion 21
        targetSdk 34

        versionCode 6
        versionName "1.4.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def KyneticsKeystorePass = System.getenv("KYNETICS_KEYSTORE_PASS")
    def KyneticsKeyAlias = System.getenv("KYNETICS_KEY_ALIAS")
    def KyneticsKeyPass = System.getenv("KYNETICS_KEY_PASS")

    def envs= [KyneticsKeystorePass, KyneticsKeyAlias, KyneticsKeyPass]
    def signWithKyneticsKey = envs.stream().allMatch(new Predicate<String>() {
        @Override
        boolean test(String s) {
            return s != null && !s.isEmpty()
        }
    })

    signingConfigs{
        if (signWithKyneticsKey) {
            kynetics {
                storePassword System.getenv("KYNETICS_KEYSTORE_PASS")
                keyAlias System.getenv("KYNETICS_KEY_ALIAS")
                keyPassword System.getenv("KYNETICS_KEY_PASS")
                storeFile file('../keystore/kynetics.jks')
                v2SigningEnabled true
                v1SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        if (signWithKyneticsKey) {
            kynetics_key {
                signingConfig signingConfigs.kynetics
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                matchingFallbacks = ['release', 'debug']
            }
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation libs.androidx.appcompat
    implementation libs.androidx.constraintlayout
    testImplementation libs.junit
    androidTestImplementation libs.androidx.junit
    androidTestImplementation libs.androidx.espresso.core
    implementation libs.androidx.cardview
    implementation libs.material
}
