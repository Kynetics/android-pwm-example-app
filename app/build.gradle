import java.util.function.Predicate

/*
 * Copyright © 2020 – 2024  Kynetics, Inc.
 * SPDX-License-Identifier: Apache-2.0
 */

apply plugin: 'com.android.application'

android {
    namespace 'com.kynetics.android_pwm_example_app'
    compileSdk 33
    buildToolsVersion "34.0.0"
    defaultConfig {
        applicationId "com.kynetics.android_pwm_example_app"
        minSdkVersion 19
        targetSdk 33
        versionCode 6
        versionName "1.4.1"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    def KyneticsKeystorePass = System.getenv("KYNETICS_KEYSTORE_PASS")
    def KyneticsKeyAlias = System.getenv("KYNETICS_KEY_ALIAS")
    def KyneticsKeyPass = System.getenv("KYNETICS_KEY_PASS")

    def envs= [KyneticsKeystorePass, KyneticsKeyAlias, KyneticsKeyPass]
    def signWithKyneticsKey = envs.stream().allMatch(new Predicate<String>() {
        @Override
        boolean test(String s) {
            return s != null && !s.isEmpty()
        }
    })

    signingConfigs{
        if (signWithKyneticsKey) {
            kynetics {
                storePassword System.getenv("KYNETICS_KEYSTORE_PASS")
                keyAlias System.getenv("KYNETICS_KEY_ALIAS")
                keyPassword System.getenv("KYNETICS_KEY_PASS")
                storeFile file('../keystore/kynetics.jks')
                v2SigningEnabled true
                v1SigningEnabled true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        if (signWithKyneticsKey) {
            kynetics_key {
                signingConfig signingConfigs.kynetics
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
                matchingFallbacks = ['release', 'debug']
            }
        }
    }

    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.aar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.google.android.material:material:1.9.0'
}
